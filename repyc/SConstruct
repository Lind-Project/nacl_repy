#Chris Matthews cmatthew@cs.uvic.ca

print "Building RePyC - Chris Matthews <cmatthew@cs.uvic.ca>"

#this is to find the correct python 2.5 or 2.6 path
import sys
version = sys.version[:3]
path = sys.exec_prefix
pathstring = path + "/include/python" + version
libstring = path + "/lib/python" + version


env = Environment()
env.Append(CCFLAGS='-Wall')
env.Append(CPPPATH = [pathstring])

env.Append(LIBS_PATH = [libstring])
env.Append(CPPPATH = ['#src'])


conf = Configure(env)
if not conf.CheckLibWithHeader('python2.6', 'Python.h', 'c'):
	if not conf.CheckLibWithHeader('python2.5', 'Python.h', 'c'):
		print 'Did not find python-dev: looking for libpython.so version 2.5 or 2.6 and Python.h, exiting!'
		Exit(1)
env = conf.Finish()

Export('env')

libs = SConscript('src/SConscript')

program = SConscript('tests/SConscript',  exports='libs')




import os
rpath = os.getenv("REPY_PATH")

installer = env.Command(rpath+'/lib/librepyc.a', [libs, program],
            "python preparetest.py -t " + rpath,
            chdir='..')
AlwaysBuild(installer)
env.Alias('install', [installer])
env.Alias('all',[libs,program])
Default(libs, program)

#valgrind = env.Command(None, [libs,program],
#		  "valgrind --leak-check=yes --gen-suppressions=no  --suppressions=repyc.supp --track-fds=yes --num-callers=50 tests/test ")

#env.Alias('valgrind',valgrind)